buildscript {
  repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
  }
  dependencies {
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"
  defaultConfig {
    applicationId 'com.karumi.katasuperheroes'
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:23.1.1'
  compile 'com.android.support:design:23.1.1'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.google.dagger:dagger:2.0.2'
  compile 'org.glassfish:javax.annotation:10.0-b28'
  apt 'com.google.dagger:dagger-compiler:2.0.2'
  testCompile 'com.google.dagger:dagger-compiler:2.0.2'
  testCompile 'junit:junit:4.12'
  androidTestCompile 'com.github.fabioCollini:DaggerMock:0.4'
  androidTestApt 'com.google.dagger:dagger-compiler:2.0.2'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  androidTestCompile 'org.mockito:mockito-core:1.10.19'
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
    exclude group: 'javax.inject', module: 'javax.inject'
    exclude group: 'com.squareup', module: 'javawriter'
  }
  androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
    exclude module: 'support-annotations'
    exclude module: 'recyclerview-v7'
    exclude module: 'support-v4'
  }
  androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.1')
}

configurations.all {
  resolutionStrategy.force 'com.android.support:support-annotations:23.0.1'
}

task grantAnimationPermission(type: Exec, dependsOn: 'installDebug') {
  commandLine "adb shell pm grant com.karumi.katasuperheroes android.permission.SET_ANIMATION_SCALE".
      split(' ')
}

tasks.whenTaskAdded { task ->
  if (task.name.startsWith('connected')) {
    task.dependsOn grantAnimationPermission
  }
}
